def sourcePattern = '**/src/**/*.kt'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:$Dependencies.ktlintVersion"
    ktlint project("static-analysis:ktlint:rules")
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--reporter=plain", "--color", "--editorconfig=${rootProject.file("static-analysis/ktlint/.editorconfig")}",  sourcePattern
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "--editorconfig=${rootProject.file("static-analysis/ktlint/.editorconfig")}", sourcePattern
}
